#machine configuration file for SP2-IMX8MP

require conf/machine/include/imx8mp-evk.inc

#
# kernel
#
IMX_KERNEL_CONFIG_AARCH64:sp2-imx8mp = "adlink_sp2imx8mp_defconfig"

DELTA_KERNEL_DEFCONFIG += "kernel-debug.config"

# The device tree name is implicit for LPDDR4, so can't use MACHINE here
KERNEL_DEVICETREE_BASENAME = "sp2-imx8mp"
KERNEL_DEVICETREE = " \
	adlink/${KERNEL_DEVICETREE_BASENAME}.dtb \
	adlink/10inch-overlay.dtbo \
	adlink/7inch-overlay.dtbo \
	adlink/7dsi-overlay.dtbo \
	adlink/awcm276masur-overlay.dtbo \
	adlink/awcm276mapur-overlay.dtbo \
	adlink/eth0-overlay.dtbo \
	adlink/cma950mb-overlay.dtbo \
	adlink/cma750mb-overlay.dtbo \
"

# over device tree overlay
EXTRA_OEMAKE:append:pn-linux-imx = " DTC_FLAGS='-@ -H epapr'"

# kernel patches
EXTRA_KERNEL_PATCHES = " \
	file://adlink_sp2imx8mp_defconfig \
	file://kernel-debug.config \
"
EXTRA_KERNEL_DTS = " \
    file://${KERNEL_DEVICETREE_BASENAME}.dts \
    file://${KERNEL_DEVICETREE_BASENAME}-otg.dts \
    file://10inch-overlay.dts \
    file://7inch-overlay.dts \
    file://7dsi-overlay.dts \
    file://awcm276masur-overlay.dts \
    file://awcm276mapur-overlay.dts \
    file://eth0-overlay.dts \
    file://cma950mb-overlay.dts \
    file://cma750mb-overlay.dts \
"

KERNEL_SRC_PATCHES[sp2-imx8mp] = " \
	${EXTRA_KERNEL_PATCHES} \
	${EXTRA_KERNEL_DTS} \
"

#
# u-boot
#
UBOOT_CONFIG_BASENAME = "adlink_sp2imx8mp"
UBOOT_CONFIG[fspi] = "${UBOOT_CONFIG_BASENAME}_defconfig"
UBOOT_DTB_NAME = "sp2imx8mp.dtb"
UBOOT_BOOT_SCRIPT = "${@bb.utils.contains('IMAGE_BASENAME', 'adlink-image-ums', 'boot.scr', 'overlay.scr', d)}"

# u-boot patches
EXTRA_UBOOT_PATCHES = " \
    file://adlink_sp2imx8mp_defconfig \
"
EXTRA_UBOOT_DTS = " \
    file://sp2imx8mp.dts \
"
UBOOT_SRC_PATCHES[sp2-imx8mp] = " \
	${EXTRA_UBOOT_PATCHES} \
	${EXTRA_UBOOT_DTS} \
"

#
# DDR FIRMWARE
#
DDR_FIRMWARE_VERSION = "202006"
DDR_FIRMWARE_NAME = " \
        lpddr4_pmu_train_1d_dmem_${DDR_FIRMWARE_VERSION}.bin \
        lpddr4_pmu_train_1d_imem_${DDR_FIRMWARE_VERSION}.bin \
        lpddr4_pmu_train_2d_dmem_${DDR_FIRMWARE_VERSION}.bin \
        lpddr4_pmu_train_2d_imem_${DDR_FIRMWARE_VERSION}.bin \
"

#
# imx-mkimage (boot target)
#
IMXBOOT_TARGETS_BASENAME = "flash_evk"

# Serial debug console
SERIAL_CONSOLES = "115200;ttymxc2"

#
# imx-boot
#
IMAGE_BOOTLOADER = "imx-boot"
UBOOT_MAKE_TARGET = ""

# imx-atf (no change from default 0x30890000)
#ATF_BOOT_UART_BASE = "0x30890000"

#
# Image Format (wic)
#
WIC_PARTITION_TABLE_TYPE = "msdos"
WIC_FSTAB_BLKDEV = "mmcblk2"
IMAGE_FSTYPES:append = " wic wic.md5sum"
WKS_FILE_DEPENDS:append = " imx-m7-demos"
IMAGE_BOOT_FILES += "imx8mp_m7_TCM_hello_world.bin \
                    imx8mp_m7_TCM_rpmsg_lite_pingpong_rtos_linux_remote.bin \
                    imx8mp_m7_TCM_rpmsg_lite_str_echo_rtos.bin \
                    imx8mp_m7_TCM_sai_low_power_audio.bin \
                    ${UBOOT_BOOT_SCRIPT}.uimg;boot.scr \
"

do_image_wic[depends] += "parted-native:do_populate_sysroot \
                          mtools-native:do_populate_sysroot \
                          dosfstools-native:do_populate_sysroot \
                          e2fsprogs-native:do_populate_sysroot \
                          u-boot-script:do_install"

